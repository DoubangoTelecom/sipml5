/*
* Copyright (C) 2012 Doubango Telecom <http://www.doubango.org>
*
* Contact: Mamadou Diop <diopmamadou(at)doubango[dot]org>
*	
* This file is part of Open Source sipML5 solution <http://www.sipml5.org>
*
* sipML5 is free software: you can redistribute it and/or modify
* it under the terms of the GNU General Public License as publishd by
* the Free Software Foundation, either version 3 of the License, or
* (at your option) any later version.
*	
* sipML5 is distributed in the hope that it will be useful,
* but WITHOUT ANY WARRANTY; without even the implied warranty of
* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
* GNU General Public License for more details.
*	
* You should have received a copy of the GNU General Public License
* along with sipML5.
*/
tsip_header_P_Preferred_Identity.prototype = Object.create(tsip_header.prototype);
%%{
	machine tsip_machine_parser_header_P_Preferred_Identity;

	# Includes
	include tsip_machine_utils "./tsip_machine_utils.jrl";
	
	action tag{
		i_tag_start = p;
	}
	
	action parse_uri{	
		if(hdr_pi && !hdr_pi.o_uri) /* Only one URI */{
			var s_uri = tsk_ragel_parser_get_string(s_str, p, i_tag_start);
			if((hdr_pi.o_uri = tsip_uri.prototype.Parse(s_uri)) && hdr_pi.s_display_name){
				hdr_pi.o_uri.s_display_name = tsk_strdup(hdr_pi.s_display_name);
			}
		}
	}

	action parse_display_name{
		if(!hdr_pi.s_display_name){
			hdr_pi.s_display_name = tsk_ragel_parser_get_string(s_str, p, i_tag_start);
            hdr_pi.s_display_name = tsk_string_unquote_2(hdr_pi.s_display_name);
		}

	}

	action eob{
	}
	
	#/* FIXME: Only one URI is added --> According to the ABNF the header could have more than one URI. */
	
	URI = (scheme HCOLON js_any+)>tag %parse_uri;
	display_name = (( token LWS )+ | quoted_string)>tag %parse_display_name;
	my_name_addr = display_name? :>LAQUOT<: URI :>RAQUOT;

	PPreferredID_value = (my_name_addr)>0 | (URI)>1;
	PPreferredID = "P-Preferred-Identity"i HCOLON PPreferredID_value>1 ( COMMA PPreferredID_value )*>0;
	
	# Entry point
	main := PPreferredID :>CRLF @eob;

}%%


%%write data;

function tsip_header_P_Preferred_Identity(o_uri){
	tsip_header.call(this, tsip_header_type_e.P_Preferred_Identity);
    this.o_uri = o_uri;
    this.s_display_name = null;
}

tsip_header_P_Preferred_Identity.prototype.toString = function(){
    if(this.o_uri){
        return tsip_uri_tostring(this.o_uri, true, true);
    }
    return null;
}

tsip_header_P_Preferred_Identity.prototype.Parse = function(s_str){
    var cs = 0;
	var p = 0;
	var pe = s_str.length;
	var eof = pe;
	var data = tsk_buff_str2ib(s_str);
	var i_tag_start;	
	var hdr_pi = new tsip_header_P_Preferred_Identity(null);
	
	%%write init;
	%%write exec;
	
	if( cs < %%{ write first_final; }%% ){
		tsk_utils_log_error("Failed to parse 'P-Preferred-Identity' header: " + s_str);
		return null;
	}
	
	return hdr_pi;
}